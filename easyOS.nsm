; haribote-os boot asm
; TAB=4

BOTPAK	EQU		0x00280000		; bootpackのロ`ド先
DSKCAC	EQU		0x00100000		; ディスクキャッシュの鏊
DSKCAC0	EQU		0x00008000		; ディスクキャッシュの鏊（リアルモ`ド）

;获取引导信息

;图像信息的位置 0x0ff0 ~0x0ff8
	CYLS EQU 0x0ff0				;定位区(在IPL.nsm中将装在区域的结束位置写入了0x0ff8中)
	LEDS EQU 0x0ff1				;LED灯
	VMODE EQU 0x0ff2			;色位数
	SCRNX EQU 0x0ff4			;分辨率(x,y)
	SCRNY EQU 0x0ff6	
	VRAM EQU 0x0ff8				;图像缓冲区(保存屏幕内容的地方起始始的位置
	
	ORG 0xc200			
	MOV AL,0x13
	MOV AH,0x00
	INT 0x10
	
	MOV BYTE [VMODE],8			;8位?色
	MOV WORD [SCRNX],320		;分辨率320*200
	MOV WORD [SCRNY],200
	MOV DWORD [VRAM],0x000a0000	;图像缓冲区开始位置
;获取LED灯的信息
	MOV AH,0x02
	INT 0x16
	MOV [LEDS],AL
fin:

; PICが一切の割りzみを受け付けないようにする
;	AT互QCの仕では、PICの初期化をするなら、
;	こいつをCLI前にやっておかないと、たまにハングアップする
;	PICの初期化はあとでやる

		MOV		AL,0xff
		OUT		0x21,AL
		NOP						; OUT命令をBAさせるとうまくいかないCNがあるらしいので
		OUT		0xa1,AL

		CLI						; さらにCPUレベルでも割りzみ禁止

; CPUから1MB以上のメモリにアクセスできるように、A20GATEをO定

		CALL	waitkbdout
		MOV		AL,0xd1
		OUT		0x64,AL
		CALL	waitkbdout
		MOV		AL,0xdf			; enable A20
		OUT		0x60,AL
		CALL	waitkbdout

; プロテクトモ`ド移行

;[INSTRSET "i486p"]				; 486の命令まで使いたいという述

		LGDT	[GDTR0]			; 憾GDTをO定
		MOV		EAX,CR0
		AND		EAX,0x7fffffff	; bit31を0にする（ペ`ジング禁止のため）
		OR		EAX,0x00000001	; bit0を1にする（プロテクトモ`ド移行のため）
		MOV		CR0,EAX
		JMP		pipelineflush
pipelineflush:
		MOV		AX,1*8			;  iみき可能セグメント32bit
		MOV		DS,AX
		MOV		ES,AX
		MOV		FS,AX
		MOV		GS,AX
		MOV		SS,AX

; bootpackの送

		MOV		ESI,bootpack	; 送元
		MOV		EDI,BOTPAK		; 送先
		MOV		ECX,512*1024/4
		CALL	memcpy

; ついでにディスクデ`タも本来の位置へ送

; まずはブ`トセクタから

		MOV		ESI,0x7c00		; 送元
		MOV		EDI,DSKCAC		; 送先
		MOV		ECX,512/4
		CALL	memcpy

; 残り全部

		MOV		ESI,DSKCAC0+512	; 送元
		MOV		EDI,DSKCAC+512	; 送先
		MOV		ECX,0
		MOV		CL,BYTE [CYLS]
		IMUL	ECX,512*18*2/4	; シリンダ数からバイト数/4にQ
		SUB		ECX,512/4		; IPLの分だけ差し引く
		CALL	memcpy

; asmheadでしなければいけないことは全部しKわったので、
;	あとはbootpackに任せる

; bootpackの起

		MOV		EBX,BOTPAK
		MOV		ECX,[EBX+16]
		ADD		ECX,3			; ECX += 3;
		SHR		ECX,2			; ECX /= 4;
		JZ		skip			; 送するべきものがない
		MOV		ESI,[EBX+20]	; 送元
		ADD		ESI,EBX
		MOV		EDI,[EBX+12]	; 送先
		CALL	memcpy
skip:
		MOV		ESP,[EBX+12]	; スタック初期
		JMP		DWORD 2*8:0x0000001b

waitkbdout:
		IN		 AL,0x64
		AND		 AL,0x02
		JNZ		waitkbdout		; ANDのY果が0でなければwaitkbdoutへ
		RET

memcpy:
		MOV		EAX,[ESI]
		ADD		ESI,4
		MOV		[EDI],EAX
		ADD		EDI,4
		SUB		ECX,1
		JNZ		memcpy			; 引き算したY果が0でなければmemcpyへ
		RET
; memcpyはアドレスサイズプリフィクスを入れ忘れなければ、ストリング命令でもける

		ALIGNB	16
GDT0:
		RESB	8				; ヌルセレクタ
		DW		0xffff,0x0000,0x9200,0x00cf	; iみき可能セグメント32bit
		DW		0xffff,0x0000,0x9a28,0x0047	; g行可能セグメント32bit（bootpack用）

		DW		0
GDTR0:
		DW		8*3-1
		DD		GDT0

		ALIGNB	16
bootpack:

